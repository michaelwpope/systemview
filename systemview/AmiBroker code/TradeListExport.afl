
/*

    Trade List Export

    This "include" file is designed to be included into an AmiBroker Formula Language (AFL) file
    to produce a Comma Separated Values (.csv) file containing a list of trades generated by the
    system, for subsequent processing outside AmiBroker

    Usage :

    ExcelFolder = "C:\\Users\\username\\";  //  note that "\" are required to be doubled
    SystemName = "sytemname";
    #include <TradeListExport.afl>

*/

SetOption("UseCustomBacktestProc", True );

if( Status( "Action" ) == actionPortfolio ) {
 
    bo = GetBacktesterObject();
    bo.Backtest();
 
    TradeListFileName = ExcelFolder + SystemName + " Trade List.csv";
    TradeListFile = fopen( TradeListFileName, "w" );
 
    if( TradeListFile ) {
         
        fputs( "Symbol,Trade,Date,Price,Ex. date,Ex. Price,% chg,Profit,% Profit,Shares,Position value,Cum. Profit,# bars,Profit/bar,MAE,MFE\n", TradeListFile );
     
        CumProfit = 0;
     
        for( Trade = bo.GetFirstTrade(); Trade; Trade = bo.GetNextTrade() ) {
         
            if( Trade.IsLong() ) { DirectionText = "Long"; } else { DirectionText = "Short"; }
            Change = ( Trade.ExitPrice - Trade.EntryPrice ) / Trade.EntryPrice * 100;
            Value = Trade.EntryPrice * Trade.Shares;
            CumProfit = CumProfit + Trade.GetProfit();
         
            String = Trade.Symbol + "," + DirectionText + ",";
            String = String + DateTimeToStr( Trade.EntryDateTime ) + "," + NumToStr( Trade.EntryPrice, 8.3, False ) + ",";
            String = String + DateTimeToStr( Trade.ExitDateTime ) + "," + NumToStr( Trade.ExitPrice, 8.3, False ) + ",";
            String = String + NumToStr( Change ) + "%," + NumToStr( Trade.GetProfit(), 8.3, False ) + "," + NumToStr( Trade.GetPercentProfit() ) + "%,";
            String = String + NumToStr( Trade.Shares, 8.3, False ) + "," + NumToStr( Value, 8.3, False ) + "," + NumToStr( CumProfit, 8.3, False ) + ",";
            String = String + NumToStr( Trade.BarsInTrade + 1 ) + "," + NumToStr( Trade.GetProfit() / Trade.BarsInTrade ) + ",";
            String = String + NumToStr( Trade.GetMAE() ) + "%," + NumToStr( Trade.GetMFE() ) + "%";
         
            fputs( String + "\n", TradeListFile );
         
        }
     
        fclose( TradeListFile );
     
    }
 
    else {
     
        bo.RawTextOutput( "Trade List file creation error" + "\tFolder :\t" + ExcelFolder + "\tFileName :\t" + SystemName + " Trade List.csv" );
     
    }
 
}

